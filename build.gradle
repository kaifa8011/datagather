apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

def VERSION = "0.6.4"

android {
    compileSdkVersion 29


    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 202108030
        versionName VERSION

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //解决 JavaDoc 中文注释生成失败的问题
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charSet', 'UTF-8')
    }

    lintOptions {
        abortOnError false // 即使有报错也不会停止打包
        checkReleaseBuilds false // 打包Release版本的时候也不进行Lint检测
    }

    repositories {
        flatDir {
            dirs 'libs'   // aar目录
        }
    }


}

task makeJar(type: Copy) {
    //删除存在的
    delete './build/libs/'
    //设置拷贝的文件
    from('./build/intermediates/intermediate-jars/release/')
    //打进jar包后的文件目录
    into('./build/libs/')
    //将classes.jar放入build/libs/目录下
    //include ,exclude参数来设置过滤
    //（我们只关心classes.jar这个文件）
    include('classes.jar')
    //重命名
    rename('classes.jar', "datagather-${VERSION}.jar")
}

makeJar.dependsOn(build)


tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding = "UTF-8"
}

publish {
    userOrg = 'kaifa8011ol'               // bintray注册的用户名
    groupId = 'com.ciba'
    artifactId = 'datagather'                // bintray创建的package
    publishVersion = VERSION
}

dependencies {
    testImplementation 'junit:junit:4.12'
    compileOnly fileTree(include: ['*.jar'], dir: 'libs')
    compileOnly 'com.android.support:appcompat-v7:28.0.0'
    api 'com.ciba:http:0.0.13'
//    releaseApi 'com.ciba:datasynchronize:0.4.3'
    api project(':datasynchronize')
    compileOnly(name: 'ADSuyiOAIDAdapter-1.0.25.08021', ext: 'aar')

}
